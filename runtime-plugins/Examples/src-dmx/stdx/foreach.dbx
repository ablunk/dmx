#import "../dbl"
#import "../stdlib"

module stdx_foreach;

extension ForEach extends dbl LocalScopeStatement {
	start ForEach;
	ForEach -> "foreach" "(" statements : list Variable "in" collection : Expression ")"
		body : LocalScopeStatement;
}

semantics for ForEach {
	Variable itemVariable = statements.get(0) as Variable;
	
	boolean collectionRefersToArray = false;
	IdExpr collectionIdExpr = collection as IdExpr;
	// usually we would check if the collectionVariable is an instanceof Variable.
	// however, no java classes are generated for the extension metamodel. therefore, each rules metaclass is mapped to the type EObject.
	//String collectionVariableName = new String(collectionIdExpr.referencedElement.eClass().name);
	//if (collectionVariableName.equals("Variable")) {
	if (collectionIdExpr.getReferencedElement() instanceof Variable) {
		Variable referencedCollectionVariable = collectionIdExpr.referencedElement as Variable;
		List arrayDimensions = referencedCollectionVariable.typeArrayDimensions;
		//if (referencedCollectionVariable.typeArrayDimensions.size() > 0) {
		if (arrayDimensions.size() > 0) {
			collectionRefersToArray = true;
		}
	}
	
	if (collectionRefersToArray) {
		ID i;
		expand "for (int " i " = 0; " i " < " collection ".length; " i "=" i "+1) {";
		expand 		itemVariable " = " collection "[" i "];";
		expand		body;
		expand "}";
	} else {
		ID it;
		ID item; 
		expand "Iterator " it " = " collection ".iterator();";
		expand "while (" it ".hasNext()) {";
		expand "	Object " item " = " it ".next();";
		expand 		itemVariable " = " item " as " itemVariable.getClassifierType() ";";
		expand		body;
		expand "}";
	}
}