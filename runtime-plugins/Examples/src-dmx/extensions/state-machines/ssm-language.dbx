#import "../../dbl"
#import "../../stdlib"

module ssm;

extension MessageAccess extends dbl L1Expr {
	start MessageAccessSyntax;
	MessageAccessSyntax -> "message";
}

semantics for MessageAccess {
	expand "event";
}

extension SendStatement extends dbl SimpleStatement {
	start SendStatementSyntax;
	SendStatementSyntax -> "send" signal:Expression "to" target:Expression ";";
}

semantics for SendStatement {
	expand target ".sendSignal(" signal ");";
}

extension StateMachine extends dbl ClassContentExtension {
	start StateMachine;
	
	StateMachine -> "stateMachine" "{" ManyRegularStates "}";
	ManyRegularStates -> ;
	ManyRegularStates -> states : list State ManyRegularStates;
	
	State -> initialModifier:InitialModifier "state" name:ID Transitions;
	InitialModifier -> ;
	InitialModifier -> initial:"initial";
	
	Transitions -> ;
	Transitions -> "{" OneOrMoreTransitions "}";
	
	OneOrMoreTransitions -> outgoing : list Transition MoreTransitions;
	MoreTransitions -> ;
	MoreTransitions -> "," OneOrMoreTransitions;

	Transition -> Trigger TargetState;
	
	// TODO can we make the messageVariable accessible inside effects?
	Trigger -> "on" messageVariable:Variable Effects;
	Trigger -> "after" timeDuration:Expression Effects;
	Effects -> ;
	Effects -> "do" effects : SimpleStatement;
		
	TargetState -> "then" "goto" target:$$State;
	TargetState -> "then" "stop";
}

semantics for StateMachine {
	expand "StateMachineRuntime createStateMachineRuntime(Object context) {";
	
	expand "StateMachine stateMachine = new StateMachine;";
	expand "stateMachine.context = context;";
	expand "StateMachineRuntime stateMachineRuntime = new StateMachineRuntime(stateMachine);";
	
	for (int i=0; i<states.size(); i=i+1) {
		State state = states.get(i) as State;
		ID stateId;
		expand "State " stateId " = new State;";
		expand "stateMachine.states.add(" stateId ");";
		expand stateId ".name = \"" state.name "\";";
		expand "stateMachine.initialState = " stateId ";";
		
		ID compiledStateId;
		expand "CompiledState " compiledStateId " = new CompiledState(" stateId ");";
		expand "stateMachineRuntime.setCompiledState(" stateId ", " compiledStateId ");";
		
		for (int t=0; t<state.outgoing.size(); t=t+1) {
			Transition transition = state.outgoing.get(t) as Transition;
			ID transitionId;
			expand "Transition " transitionId " = new Transition;";
			expand stateId ".outgoingTransitions.add(" transitionId ");";
			expand transitionId ".target = " stateId ";";

			ID transitionEffectsFunctionId;
			// TODO actually "in" means "after"
			expand function 
				"void " transitionEffectsFunctionId "() {"
				transition.effects
				"}"
				in self;

			EObject containerModule = getContainer("Module", self);
			Class containerClass = getContainer("Class", self) as Class;
			
			Variable signalVariable = transition.messageVariable;
			IdExpr signalType = signalVariable.classifierType;
			
			ID compiledTransitionClassId;
			expand class
				"class " compiledTransitionClassId " extends CompiledTransition {"
					"new(Transition transition) {"
					"	super(transition);"
					"}"
					"boolean canHandleSignal(Signal signal) {"
					"	return signal instanceof " signalType ";" 
					"}"
					"void executeEffects(Object context, Event event) {"
					"	" containerClass.name " concreteContext = context as " containerClass.name ";"
					"	concreteContext." transitionEffectsFunctionId "();"
					"}"
				"}"
				in containerModule;

			ID compiledTransitionId;
			expand "CompiledTransition " compiledTransitionId " = new " compiledTransitionClassId;
			expand "(" transitionId ");";
			expand compiledStateId ".addCompiledTransition(" compiledTransitionId ");";
		}
	}

	expand "return stateMachineRuntime;";
	
	expand "}";
}

// cannot cast to Module because of dynamic eobjects
EObject getContainer(string containerName, EObject eObject) {
	if (eObject != null) {
		String eClassName = new String(eObject.eClass().getName());
		if (eClassName.equals(containerName)) {
			return eObject;
		} else {
			return getContainer(containerName, eObject.eContainer());
		}
	} else {
		return null;
	}
}
